plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
archivesBaseName = project.mod_name
version = project.mod_version
group = project.mod_group

loom {
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
}

dependencies {
    final libs = [
            "com.github.frontear:Efkolia:+"
    ]

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    //noinspection GradlePackageVersionRange
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    libs.each {
        modImplementation(it) {
            transitive = false
        }

        include it
    }
}

processResources {
    final replacements = [
            "fabric.mod.json": [
                    "version": project.version,
                    "loader" : project.loader_version
            ],
    ]

    replacements.each { file, props ->
        filesMatching(file) {
            expand props
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}